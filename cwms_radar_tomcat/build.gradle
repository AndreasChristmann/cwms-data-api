apply plugin: 'war'

configurations {
    info
    webjars
}

dependencies {
    testImplementation 'com.github.h-thurow:tomcat8jndi:1.0.0'
    testImplementation 'org.apache.tomcat:tomcat-dbcp:8.0.47'
    implementation (project(':cwms_radar_api')) {
        exclude group: 'org.eclipse.jetty'
        exclude group: 'org.eclipse.jetty.websocket'
    }
    info (project(':cwms_radar_api')) {
        exclude group: '*'
    }
    compileOnly "javax:javaee-web-api:$JAVA_EE_WEB_VERSION"

    implementation("io.javalin:javalin:$JAVALIN_VERSION") {
        exclude group: "org.eclipse.jetty"
        exclude group: "org.eclipse.jetty.websocket"
    }
    implementation("io.javalin:javalin-openapi:$JAVALIN_VERSION") {
        //exclude group: "org.eclipse.jetty"
        exclude group: "org.eclipse.jetty.websocket"
        exclude group: "jakarta.xml.bind", module: "*"
    }
    implementation( "io.swagger.core.v3:swagger-core:$SWAGGER_CORE_VERSION"){
        exclude group: "jakarta.xml.bind", module: "*"
    }

    implementation 'org.slf4j:slf4j-simple:1.7.28'
    implementation "io.dropwizard.metrics:metrics-core:$dropwizard_version"
    implementation "io.dropwizard.metrics:metrics-servlets:$dropwizard_version"
    implementation "com.googlecode.owasp-java-html-sanitizer:owasp-java-html-sanitizer:$OWASP_SANITIZER_VERSION"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:$JACKSON_VERSION"
    runtimeOnly 'jstl:jstl:1.2'

    testImplementation 'com.oracle.ojdbc:ojdbc8:19.3.0.0'

    webjars( 'org.webjars:swagger-ui:3.52.3' ){
        transitive = false
    }

}

task extractInfo( type: Copy ){
    from zipTree(configurations.info.singleFile)
    into file("$buildDir/extra")
    include "/static/**"
    eachFile {
        def segments = it.getRelativePath().getSegments() as List
        it.setPath(segments.tail().join("/"))
        return it
    }

    includeEmptyDirs false
}

task extractWebJars( type: Copy ){
    doFirst {
        project.mkdir("$buildDir/extra/swagger-ui")
    }
    from zipTree(configurations.webjars.singleFile)
    into file("$buildDir/extra")
    includeEmptyDirs false
    eachFile {
        path -= ~/^.+?\/.+?\/.+?\/.+?\/.+?\//
    }
    exclude "**/index.html*"
    include "/META-INF/resources/webjars/swagger-ui/**"
    includeEmptyDirs = false
}

war {
    dependsOn extractInfo
    dependsOn extractWebJars
    from "$buildDir/extra"
    from "src/resources/"

    doLast {
        println( "::set-output name=WARFILE::${project.name}-${project.version}.war")
    }
}


test {
	useJUnitPlatform()
}

description = 'CWMS RADAR Tomcat Instance'
